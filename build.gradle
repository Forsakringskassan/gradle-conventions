/**
 * Used when working with this plugin.
 */
plugins {
	id "groovy-gradle-plugin"
	id "io.github.gradle-nexus.publish-plugin" version "2.0.0" apply false
	id "se.bjurr.gitchangelog.git-changelog-gradle-plugin" version "3.1.1" apply false
	id "com.diffplug.spotless" version "8.0.0"
	id "se.bjurr.gradle.update-versions" version "1.5.2"
}

/**
 * Dependencies needed by the plugin in src/main/groovy
 */
dependencies {
	implementation "io.github.gradle-nexus:publish-plugin:2.0.0"
	implementation "se.bjurr.gitchangelog:git-changelog-gradle-plugin:3.1.1"
	implementation "se.bjurr.gradle.update-versions:se.bjurr.gradle.update-versions.gradle.plugin:1.5.1"
	implementation "se.bjurr.violations:violations-lib:1.160.1"
	implementation "com.diffplug.spotless:spotless-plugin-gradle:8.0.0"
	implementation "se.bjurr.violations:violations-gradle-plugin:4.1.0"
	implementation "com.github.spotbugs.snom:spotbugs-gradle-plugin:6.4.2"
	implementation "org.openapitools:openapi-generator-gradle-plugin:7.16.0"
}

repositories {
	gradlePluginPortal()
	mavenCentral()
	mavenLocal()
}

sourceCompatibility = 17
targetCompatibility = 17

spotless {
	groovyGradle {
		target '*.gradle', '**/*.gradle'
		greclipse()
	}
}
processResources.dependsOn spotlessApply

apply from: "src/main/groovy/se.fk.gradle.pom.gradle"
apply from: "src/main/groovy/se.fk.gradle.publish-java.gradle"
apply from: "src/main/groovy/se.fk.gradle.release.gradle"
apply from: "src/main/groovy/se.fk.gradle.sign.gradle"

tasks.named("release") {
	tasks = [
		'clean',
		"publishToMavenLocal",
		'gitChangelog',
		'commitChangelogTask',
		"publish",
		"closeAndReleaseStagingRepositories"
	]
}
