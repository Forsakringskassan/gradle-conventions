/**
 * Used when working with this plugin.
 */
plugins {
	id "groovy-gradle-plugin"
	id "io.github.gradle-nexus.publish-plugin" version "2.0.0" apply false
	id "com.gradle.plugin-publish" version "2.0.0" apply false
	id "se.bjurr.gitchangelog.git-changelog-gradle-plugin" version "3.0.2" apply false
	id "com.diffplug.spotless" version "6.25.0"
	id "se.bjurr.gradle.update-versions" version "1.2.0"
}

/**
 * Dependencies needed by the plugin in src/main/groovy
 */
dependencies {
	implementation "com.gradleup.shadow:shadow-gradle-plugin:9.1.0"
	implementation "com.gradleup:gr8-plugin:0.11.2"
	implementation "io.github.gradle-nexus:publish-plugin:2.0.0"
	implementation "com.gradle.publish:plugin-publish-plugin:2.0.0"
	implementation "se.bjurr.gitchangelog:git-changelog-gradle-plugin:3.0.2"
	implementation "se.bjurr.gradle.update-versions:se.bjurr.gradle.update-versions.gradle.plugin:1.2.0"
	implementation "se.bjurr.violations:violations-lib:1.158.1"
	implementation "com.diffplug.spotless:spotless-plugin-gradle:6.25.0"
	implementation 'se.bjurr.violations:violations-gradle-plugin:3.0.4'
	implementation 'com.github.spotbugs.snom:spotbugs-gradle-plugin:6.0.26'
}

repositories {
	gradlePluginPortal()
	mavenCentral()
	mavenLocal()
}

sourceCompatibility = 17
targetCompatibility = 17

spotless {
	groovyGradle {
		target '*.gradle', '**/*.gradle'
		greclipse()
	}
}
processResources.dependsOn spotlessApply

apply from: "src/main/groovy/se.bjurr.gradle.pom.gradle"
apply from: "src/main/groovy/se.bjurr.gradle.publish-java.gradle"
apply from: "src/main/groovy/se.bjurr.gradle.release.gradle"


tasks.named("release") {
	tasks = [
		'clean',
		'gitChangelogSemanticVersion',
		'commitNewVersionTask',
		"publishToMavenLocal",
		'gitChangelog',
		'commitChangelogTask',
		"publish",
		"closeAndReleaseStagingRepositories"
	]
}
