/**
 * Used when working with this plugin.
 */
plugins {
	id "groovy-gradle-plugin"
	id "se.bjurr.gitchangelog.git-changelog-gradle-plugin" version "3.1.1" apply false
	id "com.diffplug.spotless" version "8.0.0"
	id "se.bjurr.gradle.update-versions" version "1.5.4"
	id "maven-publish"
}

/**
 * Dependencies needed by the plugin in src/main/groovy
 */
dependencies {
	implementation "se.bjurr.gitchangelog:git-changelog-gradle-plugin:3.1.1"
	implementation "se.bjurr.gradle.update-versions:se.bjurr.gradle.update-versions.gradle.plugin:1.5.5"
	implementation "se.bjurr.violations:violations-lib:1.160.2"
	implementation "com.diffplug.spotless:spotless-plugin-gradle:8.0.0"
	implementation "se.bjurr.violations:violations-gradle-plugin:4.1.2"
	implementation "com.github.spotbugs.snom:spotbugs-gradle-plugin:6.4.4"
	implementation "org.openapitools:openapi-generator-gradle-plugin:7.16.0"
	implementation 'org.yaml:snakeyaml:2.5'
}

repositories {
	gradlePluginPortal()
	mavenCentral()
	mavenLocal()
}

java {
	def javaVersion = JavaLanguageVersion.of(21)
	withSourcesJar()
	withJavadocJar()
	toolchain {
		languageVersion = javaVersion
	}
	sourceCompatibility = javaVersion
	targetCompatibility = javaVersion
}

spotless {
	groovyGradle {
		target '*.gradle', '**/*.gradle'
		greclipse()
	}
}
processResources.dependsOn spotlessApply

publishing {
	repositories {
		maven {
			name = "GitHubPackages"
			def repo = "Forsakringskassan/repository"
			url = uri("https://maven.pkg.github.com/${repo}")
			credentials {
				username = project.findProperty("GITHUB_ACTOR") ?: System.getenv("PACKAGES_RW_ACTOR")
				password = project.findProperty("GITHUB_TOKEN") ?: System.getenv("PACKAGES_RW_TOKEN")
			}
		}
	}
}

apply from: "src/main/groovy/se.fk.gradle.pom.gradle"
apply from: "src/main/groovy/se.fk.gradle.release.gradle"

tasks.withType(Javadoc) {
	options.addStringOption('Xdoclint:none', '-quiet')
}


tasks.named("release") {
	tasks = [
		'clean',
		"publishToMavenLocal",
		'gitChangelog',
		'commitChangelogTask',
		"publish"
	]
}
