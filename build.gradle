plugins {
	id "groovy-gradle-plugin"
	id "com.gradle.plugin-publish" version "1.3.0"
	id "se.bjurr.gitchangelog.git-changelog-gradle-plugin" version "2.1.2"
	id "com.diffplug.spotless" version "6.25.0"
}

check.dependsOn spotlessApply

spotless {
	groovyGradle {
		target '*.gradle', '**/*.gradle'
		greclipse()
	}
}

gitChangelogSemanticVersion {
	suffixSnapshotIfNotTagged = false
}

gradlePlugin {
	website = "https://github.com/tomasbjerre/conventional-release-gradle-plugin"
	vcsUrl = "https://github.com/tomasbjerre/conventional-release-gradle-plugin"
	plugins {
		matching { it.name == 'se.bjurr.gradle.conventional-release' }.configureEach {
			id = "se.bjurr.gradle.conventional-release"
			implementationClass = "SeBjurrGradleConventionalReleasePlugin"
			displayName = "Conventional Release Gradle Plugin"
			description = "Bundle of plugins and some Gradle DSL that can publish to Central, NPM and Plugin Portal"
			tags.set([
				"conventional-commits",
				"changelog",
				"publishing",
				"release"
			])
		}
	}
}

repositories {
	gradlePluginPortal()
	mavenCentral()
	mavenLocal()
}

task release(type: GradleBuild) {
	tasks = [
		'clean',
		'gitChangelogSemanticVersion',
		'commitNewVersionTask',
		'build',
		'publishToMavenLocal',
		'publishPlugins',
		'gitChangelog',
		'commitChangelogTask',
	]
}

/**
 * Dependencies needed by the plugin in src/main/groovy
 */
dependencies {
	implementation "com.gradleup.shadow:shadow-gradle-plugin:8.3.2"
	implementation "io.github.gradle-nexus:publish-plugin:2.0.0"
	implementation "com.gradle.publish:plugin-publish-plugin:1.3.0"
	implementation "se.bjurr.gitchangelog:git-changelog-gradle-plugin:2.1.2"
}


/**
 * Update README with config
 */
def mainGradle = 'src/main/groovy/se.bjurr.gradle.conventional-release.gradle'

def mainGradleContent = file(mainGradle).text
def defaultConfigStart = "// ---- start of default config ----"
def defaultConfigEnd = "// ---- end of default config ----"
def defaultConfig = mainGradleContent.substring(mainGradleContent.indexOf(defaultConfigStart)+defaultConfigStart.size())
defaultConfig = defaultConfig.substring(0, defaultConfig.indexOf(defaultConfigEnd))

def readmeContent = file('README.md').text
def readmeContentStart = "<!-- start default config -->"
def readmeContentEnd = "<!-- end default config -->"
def readmeContentFirstPart = readmeContent.split(readmeContentStart)[0]
def readmeContentSecondPart = readmeContent.split(readmeContentEnd)[1]
def newReadmeContent = """${readmeContentFirstPart}${readmeContentStart}
```groovy
${defaultConfig}
```
${readmeContentEnd}${readmeContentSecondPart}"""
file('README.md').write(newReadmeContent)